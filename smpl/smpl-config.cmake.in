# Config file for the smpl package
# The following variables are defined:
# smpl_INCLUDE_DIRS
# smpl_LIBRARIES

# get the path to this (installed) cmake file
get_filename_component(SMPL_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
set(smpl_INCLUDE_DIRS "@CONF_INCLUDE_DIRS@")

#if (NOT TARGET smpl AND NOT smpl_BINARY_DIR)
include(${SMPL_CMAKE_DIR}/smpl-targets.cmake)
#endif()

find_package(sbpl REQUIRED)

# Shamelessly copied from GLEW, which similarly uses GLEW::GLEW to refer to
# GLEW::glew or GLEW::glew_s shared and static library targets.
function(copy_imported_target_properties src_target dest_target)
    set(config_dependent_props
        IMPORTED_IMPLIB
        IMPORTED_LINK_DEPENDENT_LIBRARIES
        IMPORTED_LINK_INTERFACE_LANGUAGES
        IMPORTED_LINK_INTERFACE_LIBRARIES
        IMPORTED_LINK_INTERFACE_MULTIPLICITY
        IMPORTED_LOCATION
        IMPORTED_NO_SONAME
        IMPORTED_SONAME)

    # copy configuration-independent properties
    foreach(
        prop
        ${config_dependent_props}
        IMPORTED_CONFIGURATIONS
        INTERFACE_AUTOUIC_OPTIONS
        INTERFACE_COMPILE_DEFINITIONS
        INTERFACE_COMPILE_FEATURES
        INTERFACE_COMPILE_OPTIONS
        INTERFACE_INCLUDE_DIRECTORIES
        INTERFACE_LINK_LIBRARIES
        INTERFACE_POSITION_INDEPENDENT_CODE
        INTERFACE_SOURCES
        INTERFACE_SYSTEM_INCLUDE_DIRECTORIES
    )
        get_property(is_set TARGET ${src_target} PROPERTY ${prop} SET)
        if(is_set)
            get_target_property(v ${src_target} ${prop})
            set_target_properties(${dest_target} PROPERTIES ${prop} "${v}")
        endif()
    endforeach()

    # copy configuration-dependent properties
    get_target_property(imported_configs ${src_target} IMPORTED_CONFIGURATIONS)

    foreach(config ${imported_configs})
        foreach(prop_prefix ${config_dependent_props})
            set(prop ${prop_prefix}_${config})
            get_property(is_set TARGET ${src_target} PROPERTY ${prop} SET)
            if(is_set)
                get_target_property(v ${src_target} ${prop})
                set_target_properties(${dest_target} PROPERTIES ${prop} "${v}")
            endif()
        endforeach()
    endforeach()
endfunction()

if(TARGET smpl::smpl-shared)
    add_library(smpl::smpl SHARED IMPORTED)
    copy_imported_target_properties(smpl::smpl-shared smpl::smpl)
elseif(TARGET smpl::smpl-static)
    add_library(smpl::smpl STATIC IMPORTED)
    copy_imported_target_properties(smpl::smpl-static smpl::smpl)
endif()

set(smpl_LIBRARIES smpl::smpl)

